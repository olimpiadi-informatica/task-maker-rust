// Grammar of the gen/GEN file format:
// It's line based, each line can be one of the following types:
//  * comment: # followed by a space, followed by anything till end of line
//  * subtask: #ST: XX where XX is a positive integer
//  * copy: #COPY: XX where XX is a path
//  * command: a list of arguments not starting with #

whitespace = _{ " " | "\t" }
non_newline = _{ !NEWLINE ~ ANY }
number = { ASCII_DIGIT+ }

word = { (!(whitespace | "#") ~ non_newline)+ }
spaces = _{ whitespace+ }

comment = { "#" ~ spaces ~ non_newline* | "#" }
risky_comment = { "#" ~ non_newline* | "#" }
subtask = { "#ST:" ~ whitespace* ~ number ~ whitespace* }
copy = { "#COPY:" ~ whitespace* ~ word ~ whitespace* }
command = { !"#" ~ whitespace* ~ word ~ (spaces ~ word)* ~ whitespace* }
empty = { whitespace* }

line = { (subtask | copy | comment | command | empty) ~ risky_comment? }

// allow the last line to be without the NEWLINE
file = { SOI ~ (line ~ NEWLINE)* ~ line ~ NEWLINE? ~ EOI }
